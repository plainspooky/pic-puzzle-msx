100 DEFINT A-Z:KEY OFF:DEFUSR0=&H156:DEFUSR1=&H41:DEFUSR2=&H44:COLOR 255,0,0*RND(-TIME)
105 _TURBOON
110 '#C-
115 SET PAGE ,0:CLS:COPY(0,192)-STEP(255,19),1 TO (0,192):LINE(0,212)-STEP(3,3),255,BF:I=0*USR2(0)
120 IF I>15 THEN 140
125 Y=212+4*RND(1):X=4*RND(1)
130 IF POINT(X,Y)=255 THEN PSET(X,Y),I:I=I+1
135 GOTO 120
140 LINE(4,212)-STEP(3,3),255,BF
145 FOR J=0 TO 3:JJ=J+212:FOR I=0 TO 3
150 K=POINT(I,JJ):Y=K\4:X=K MOD 4
155 IF POINT(I,JJ)=POINT(4+I,JJ) THEN 165
160 IF K<>15 THEN COPY(64*X+1,48*Y+1)-STEP(61,45),1 TO (64*I+1,48*J+1),0 ELSE LINE(64*I+1,48*J+1)-STEP(61,45),146,BF:SI=J*4+I:SX=I:SY=JJ:GOSUB 220
165 NEXT I%:NEXT J%
170 K=0:L=-1:FOR J=212 TO 215:FOR I=0 TO 3:L=L AND (POINT(I,J)=K):K=K+1:NEXT I,J:IF L THEN 275
175 COPY(0,212)-STEP(3,3) TO (4,212)
180 K$=INKEY$:IF K$="" THEN 180
185 IF K$=CHR$(27) THEN GOSUB 250
190 IF K$=CHR$(28) AND SX<3 THEN PSET(SX,SY),POINT(SX+1,SY):PSET(SX+1,SY),15
195 IF K$=CHR$(29) AND SX>0 THEN PSET(SX,SY),POINT(SX-1,SY):PSET(SX-1,SY),15
200 IF K$=CHR$(30) AND SY>212 THEN PSET(SX,SY),POINT(SX,SY-1):PSET(SX,SY-1),15
205 IF K$=CHR$(31) AND SY<215 THEN PSET(SX,SY),POINT(SX,SY+1):PSET(SX,SY+1),15
210 IF K$=CHR$(32) THEN GOSUB 265
215 GOTO 145
220 AX=64*I:AY=48*J
225 IF I>0 THEN LINE (AX+9,AY+20)-STEP(-7,3):LINE-STEP(0,1):LINE-STEP(7,3):LINE-STEP(0,-7)
230 IF I<3 THEN LINE (AX+54,AY+20)-STEP(7,3):LINE-STEP(0,1):LINE-STEP(-7,3):LINE-STEP(0,-7)
235 IF J>0 THEN LINE (AX+28,AY+9)-STEP(3,-7):LINE-STEP(1,0):LINE-STEP(3,7):LINE-STEP(-7,0)
240 IF J<3 THEN LINE (AX+28,AY+38)-STEP(3,7):LINE-STEP(1,0):LINE-STEP(3,-7):LINE-STEP(-7,0)
245 RETURN
250 I=USR0(0):COLOR ,,28
255 K$=INKEY$:IF K$="" THEN 255
260 IF K$=CHR$(27) THEN 320 ELSE COLOR ,,0:RETURN
265 SET PAGE 1
270 K$=INKEY$:IF K$=CHR$(32) THEN SET PAGE 0:RETURN ELSE 270
275 COPY(193,145)-STEP(61,45),1 TO (193,145),0:I=0*USR0(0):IF (VDP(10) AND 2)=0 THEN S=6 ELSE S=5
280 TIME=0:LINE(0,I)-STEP(255,7),224,BF,AND:I=I+8
285 IF TIME<S THEN 285
290 IF I<191 AND INKEY$="" THEN 280
295 COPY(0,0)-STEP(255,191),1 TO (0,0),0:LINE(0,192)-STEP(255,19),255,BF,XOR
300 I=USR0(0):COLOR ,,0
305 K$=INKEY$:IF K$="" THEN 305
310 IF K$=CHR$(13) THEN 115
315 IF K$<>CHR$(27) THEN 305
320 _TURBOOFF
325 RUN "MENU.BAS"
